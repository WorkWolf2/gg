plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.16"

    id("com.gradleup.shadow") version "9.0.0-beta12"
}

group = 'com.minegolem'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }

    maven {
        name = "tcoded-releases"
        url = "https://repo.tcoded.com/releases"
    }
}

dependencies {
    paperweight.foliaDevBundle("1.21.4-R0.1-SNAPSHOT")

    implementation("com.tcoded:FoliaLib:0.5.1")

    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.3.3'

    compileOnly 'me.clip:placeholderapi:2.11.7'
    compileOnly files("libs/HypingTeams-1.2.1.jar")

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

shadowJar {
    archiveClassifier.set('')

    relocate("com.tcoded.folialib", "com.minegolem.hypingNations.lib.folialib")
    relocate 'com.zaxxer.hikari', 'com.minegolem.hypingNations.lib.hikari'
}

tasks.build.dependsOn shadowJar

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
